■更新情報をユーザに知らせたい

小さな更新では、ページを訪れた時にどこが更新されたのかわからない場合があるものです。
「何か変更はないかな？」と散々探した挙句に、何も更新されていなかったことがわかった時は、やはり悔しいものでしょう。
もちろん、現在では新着マークをつけることで新しい更新があったことをアピールしているサイトも多くなりましたが、制作者にとってはやはり面倒な作業ですし、数多くページを管理しているうちに、そんな小さなことは忘れてしまうかもしれません。
また、サイトを訪れる人はさまざまです。新着マークがついていても既に見てしまった人もいるかもしれませんし、かなり古いコンテンツであってもまだ見ていない人がいるかもしれません。
そこで、この項では、サイトを訪れた人が前にそのページを見た時点から何か更新があった時には、メッセージボックスで更新をしらせてくれる機能を紹介します。

●ポイント
　ユーザ管理の上で、もっとも注目すべきオブジェクトはSessionオブジェクトですが、この場合は残念ながら使えません。なぜならば、今回の場合、一旦は接続を切ってしまったユーザまで追跡しなければならないからです。Sessionオブジェクトはある一定時間の持続した接続の中でのユーザ管理はできますが、このように、間に何日も（あるいは何ヶ月も）おいてしまうようなユーザまで追跡することはできません。
　そこで登場するのが、クッキーと呼ばれるしくみです。

●サンプル
□cookie.asp
1	<%@Language="VBScript" %>
2	<html>
3	<%
4	Set fs=Server.CreateObject("Scripting.FileSystemObject")
#　FileSystemObjectオブジェクトを生成します。
5	Set fl=fs.GetFile(Server.MapPath("cookie.asp"))
#　自分のパスを指定し、Fileオブジェクトを生成します。
6	dte=fl.DateLastModified
#　自分（cookie.asp）の最終更新日を、変数dteに格納します。
7	If Request.Cookies("lastmodify")="" Then
8	　Response.Cookies("lastmodify")=dte
9	　Response.Cookies("lastmodify").Expires=DateAdd("yyyy",5,Date)
10	　str="はじめまして！ ようこそいらっしゃいました。"
11	Else
12	　oldDte=Request.Cookies("lastmodify")
13	　If CDate(oldDte)<CDate(dte) Then
14	　　Response.Cookies("lastmodify")=dte
15	　　Response.Cookies("lastmodify").Expires=DateAdd("yyyy",5,Date)
16	　　str="前のアクセスから更新しました。ごゆっくりごらんください。"
17	　Else
18	　　str="更新サボってました。申し訳ありません。m(_ _)m"
19	　End If
20	End If
21	%>
22	<head>
23	<title>更新さぼってませんか？</title>
24	<script language="JavaScript">
25	　window.alert("<%=str %>");
26	</script>
#　上で得た変数strの値を、クライアントサイドスクリプトのalertメソッドに渡します。ASPで得た結果をクライアントサイドのスクリプトで実行するには、このような手法をとります。
#　イレギュラーな手法に思われるかもしれませんが、本来はこのようにサーバで実現する処理とクライアントで実現する処理とをうまく配分することが、クライアント—サーバシステムの真髄であるはずなのです。汎用的な定石として覚えておくとよいでしょう。
27	</head>
28	<body>
29	最終更新日：<%=dte %>
30	</body>
31	</html>

●TIPS
△サンプル試行時の注意
　「前のアクセスから更新しました。ごゆっくりごらんください。」というメッセージダイアログを見たい場合は、cookie.aspをエディタなどで開き、上書き保存します。これによって、ファイル更新日が変更され、ファイルは更新されたと見なされます。
